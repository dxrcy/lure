use std

#: {string} -> int | never
func main(args)
    print("=== HANGMAN ===")

    if len(args) < 1 then
        print("Please provide a file path.")
        return 1
    end
    let filename = args[1]

    let words = read_file_lines(filename)
    if words == nil then
        print("Failed to read file.")
        return 2
    end

    print("\n\n\n\n\n")

    while true do
        let index = math.random(1, len(words))
        let word = words[index]

        let correct = {}
        let incorrect = {}

        while true do
            for _ in 1 to 5 do
                put("\033[A\033[K")
            end

            let visible = ""
            let is_win = true
            for _, ch in word do
                if correct[ch] then
                    visible ++= ch
                else
                    visible ++= '_'
                    is_win = false
                end
            end

            if is_win then
                print("---------")
                print("You win! :)")
                printf("The word was: '{}'\n", word)
                print("---------")
                _ = io.read()
                break
            elif len(incorrect) >= 6 then
                print("---------")
                print("You lose! :(")
                printf("The word was: '{}'\n", word)
                print("---------")
                _ = io.read()
                break
            end

            print(visible)
            printf("Chances: {}\n", 6 - len(incorrect))
            printf("Correct: ")
            print_set(correct)
            printf("Inorrect: ")
            print_set(incorrect)
            put("Guess: ")

            let line = io.read()
            if len(line) >= 1 then
                let guess = line[0]

                if word.contains(guess) then
                    if not correct[guess] then
                        correct[guess] = true
                    end
                else
                    if not incorrect[guess] then
                        incorrect[guess] = true
                    end
                end
            end
        end
    end
end

#: { char:bool } -> void
func print_set(set)
    for i, ch in set do
        if i > 0 then
            put(", ")
        end
        put(ch)
    end
    print()
end
