func main(args)
    println("=== HANGMAN ===")

    if len(args) < 1 then
        println("Please provide a file path.")
        return 1
    end
    let filename = args[1]

    let words = read_file_lines(filename)
    if words == nil then
        println("Failed to read file.")
        return 2
    end

    println("\n\n\n\n\n")

    while true do
        let index = math.random_int(0, len(words))
        let word = words[index]

        let correct = {}
        let incorrect = {}

        while true do
            for _ in 0 to 5 do
                print("\033[A\033[K")
            end

            let visible = ""
            let is_win = true
            for _, ch in word do
                if correct[ch] then
                    visible = visible & ch
                else
                    visible = visible & '_'
                    is_win = false
                end
            end

            if is_win then
                println("---------")
                println("You win! :)")
                println("The word was: '{}'\n", word)
                println("---------")
                _ = io.scan_line()
                break
            elif len(incorrect) >= 6 then
                println("---------")
                println("You lose! :(")
                println("The word was: '{}'\n", word)
                println("---------")
                _ = io.scan_line()
                break
            end

            println(visible)
            println("Chances: {}\n", 6 - len(incorrect))
            println("Correct: ")
            print_set(correct)
            println("Inorrect: ")
            print_set(incorrect)
            print("Guess: ")

            let line = io.scan_line()
            if len(line) >= 1 then
                let guess = line[0]

                if word.contains(guess) then
                    if not correct[guess] then
                        correct[guess] = true
                    end
                else
                    if not incorrect[guess] then
                        incorrect[guess] = true
                    end
                end
            end
        end
    end
end

#: string -> { string } | nil
func read_file_lines(filename)
    let result = File.open(filename, "r")
    let file = result.ok
    if result.err != nil then
        return nil
    end

    let lines = {}
    while true do
        let err, line = file.read_line()
        if err != nil then
            return nil
        end
        if line == nil then
            break
        end
        if line[-1] == '\r' then
            line = line[0, -1]
        end
        lines.push(line)
    end

    file.close()
    return lines
end

#: { char:bool } -> void
func print_set(set)
    for i, ch in set do
        if i > 0 then
            print(", ")
        end
        print(ch)
    end
    println()
end


